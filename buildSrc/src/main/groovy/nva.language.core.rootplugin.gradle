plugins{
    id 'io.github.gradle-nexus.publish-plugin'
    id 'nva.language.core.java-conventions'
    id 'nva.language.core.gradlelint'
}

allprojects {
    //workaround for jacoco-merge to work
    apply plugin: 'nva.language.core.java-conventions'
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty("sonatypeUsername")
            password = findProperty("sonatypePassword")
            // stagingProfileId = "5ba11e4895739"
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }

    clientTimeout = Duration.ofSeconds(600)
    connectTimeout = Duration.ofSeconds(60)
}

dependencies {
    subprojects.forEach {
        jacocoAggregation it
    }
}


reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
        integrationTestCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.INTEGRATION_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    dependsOn tasks.named('integrationTestCodeCoverageReport', JacocoReport)
}
